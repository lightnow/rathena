-	script	NPCKillEvent	-1,{
	end;
	
OnNPCKillEvent:
	if(!IsTownMap()
			&& (strcharinfo(3) != "06guild_01")
			&& playerattached()){
		getunitdata(killedgid,.@deadMonster);
		
		if(.@deadMonster[UMOB_MASTERAID] > 0)
		end;
		
		.@monsterLevel = .@deadMonster[UMOB_LEVEL];
		.@monsterRank = .@deadMonster[UMOB_DYNAMIC];
		
		// Trader
		// Random chance to spawn (If not spawn yet)
		.@isTraderHad = getd("$@trader" + strcharinfo(3));
		.@isTraderFull = getd("$@traderFull" + strcharinfo(3));
		if((!.@isTraderHad || .@isTraderFull)
				&& (rand(10000) <= .traderSpawnChance)){
			// Get monster drops
			getmobdrops(killedrid);
			
			// Store random drop item
			.@monsterDropItemAmount = $@MobDrop_count;
			copyarray .@monsterDropItems[0],$@MobDrop_item[0],.@monsterDropItemAmount;
			copyarray .@monsterDropitemRates[0],$@MobDrop_rate[0],.@monsterDropItemAmount;
			
			// Random index
			.@randomDropIndex = rand(.@monsterDropItemAmount);
			
			// Getting item id
			.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
			// Getting item type
			.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			
			// Do not use equipment as requirement item id
			.@retry = .@monsterDropItemAmount;
			while((.@retry > 0)
			&& ((.@type == 4) || (.@type == 5) || (.@type == 12))){
				.@retry--;
				
				// Random index
				.@randomDropIndex = rand(.@monsterDropItemAmount);
				// Getting item id
				.@monsterDropItemId = .@monsterDropItems[.@randomDropIndex];
				// Getting item type
				.@type = getiteminfo(.@monsterDropItemId,ITEMINFO_TYPE);
			}
			
			.@monsterDropItemRate = cap_value(.@monsterDropitemRates[.@randomDropIndex] / 2,1,50);
			
			if(!IsSellInShop(.@monsterDropItemId)){
				set getd("$@traderItemId" + strcharinfo(3)),.@monsterDropItemId;
				set getd("$@traderItemBaseAmount" + strcharinfo(3)),.@monsterDropItemRate;
				set getd("$@traderTradeAmount" + strcharinfo(3)),0;
				// Restart trading
				if(getd("$@traderFull" + strcharinfo(3))){
					set getd("$@traderFull" + strcharinfo(3)),0;
					set getd("$@traderLastTalk" + strcharinfo(3)),0;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ Reset รางวัลแล้ว!",bc_map;
				}
				else{
					// Set as spawned (Disappear when reload scripts)
					set getd("$@trader" + strcharinfo(3)),1;
					// Random position in current map
					getfreecell strcharinfo(3),.@x,.@y;
					set getd("$@traderX" + strcharinfo(3)),.@x;
					set getd("$@traderY" + strcharinfo(3)),.@y;
					// Spawn now
					duplicate "พ่อค้าหน้าเลือด#THE_BOX",strcharinfo(3),.@x,.@y;
					// Mark on minimap
					viewpointmap strcharinfo(3),1,.@x,.@y,1,0xEEF11E;
					announce "พ่อค้าหน้าเลือด (THE BOX) ได้ปรากฎใน Map แล้ว!",bc_map;
				}
			}
		}
		
		// Macro Detector
		.@killCountToMacro = (100 * (@stackMacroDetecting + 1));
		
		#macroDetector = cap_value(#macroDetector + 1,0,.@killCountToMacro);
		
		if((#macroDetector >= .@killCountToMacro)
				&& !@isMacroDetecting){
			duplicate_dynamic("ตรวจ MACRO#SELF");
			
			specialeffect2 644;
			
			addtimer 30000,strnpcinfo(3) + "::OnMacro";
			
			announce "โปรดกด NPC ตรวจ Macro ภายใน 30 วินาที ไม่งั้นคุณจะเหนื่อย",bc_self;
			
			@isMacroDetecting = 1;
		}
		
		if(.@monsterLevel >= BaseLevel)
		latestCombo = .@monsterRank;
		else
		latestCombo = 0;
		
		// Map Limitation
		.@lastFoundMapAmount = getarraysize(lastFoundTheBoxMap$);
		.@currentMapLimitation = 0;
		if(.@lastFoundMapAmount > 0){
			for(.@i = 0; .@i < .@lastFoundMapAmount; .@i++){
				if(lastFoundTheBoxMap$[.@i] == strcharinfo(3)){
					.@isFoundSameMap = 1;
					
					.@currentMapLimitation = mapLimitation[.@i];
					
					break;
				}
			}
		}
		
		// Chance
		.@monsterLevelBonus = cap_value(
		(.@monsterLevel / .monsterLevelDivider) * 100
		,1
		,INT_MAX);
		
		.@mapLimitation = cap_value(
		.@currentMapLimitation * .staminaDecrease
		,0
		,.baseChance);
		
		.@baseCalculated = (.baseChance * .@monsterRank);
		
		.@calculated = (.@baseCalculated - .@mapLimitation);
		
		.@chance = ((.baseChance - .@mapLimitation) > 0)
		? (.@calculated + .@monsterLevelBonus)
		: 0;
		
		/*dispbottom "Base: "
		+ .@baseCalculated
		+ " | Penalty: "
		+ (.@currentMapLimitation * .staminaDecrease)
		+ " | Calculated: "
		+ .@calculated
		+ " | Monster Bonus: "
		+ .@monsterLevelBonus
		+ " | Finalize: " + .@chance;*/
		
		if(.@chance <= 0)
		end;
		
		.@rand = rand(10000);
		
		if(#isShowTheBoxRate)
		dispbottom "ต้องสุ่มได้น้อยกว่าหรือเท่ากับ " + .@chance + " คุณสุ่มได้ " + .@rand;
		
		if(.@rand <= .@chance){
			if(!.@isFoundSameMap){
				// Remove first map if amount > Maximum
				if(.@lastFoundMapAmount >= .sameMapAmount){
					deletearray lastFoundTheBoxMap$[0],1;
					deletearray mapLimitation[0],1;
				}
			}
			
			isReadyToClaimed = 1;
			
			// Show now
			duplicate_dynamic("THE BOX#SELF");
			
			specialeffect2 747;
		}
	}

	end;
	
OnMacro:
	announce "คุณไม่ได้กด NPC ตรวจ Macro..",bc_self;
	@isMacroDetecting = 0;
	@stackMacroDetecting = 0;
	tired = cap_value(tired + 100,0,100);
	RefreshDebuff();
	end;
	
OnInit:
	.traderSpawnChance = 100;	// 10000 = 100%
	.baseChance = 100;			// 10000 = 100%
	.staminaDecrease = 20;		// 10000 = 100%
	.monsterLevelDivider = 10;	// Use to increase chance to found THE BOX (Monster Level / n)
	.sameMapAmount = 3;
	$maximumMapLimitation = 5;
	
	addrid(0);
	if(@isMacroDetecting){
		deltimer "NPCKillEvent::OnMacro";
		@isMacroDetecting = 0;
	}
	end;
}
