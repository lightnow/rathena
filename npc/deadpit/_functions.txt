/// Show current DEADPIT Points
function	script	CleanUpMap	{
	// Clean up map if users <= 1
	.@currentMapUsers = getmapusers(strcharinfo(3));
	if(.@currentMapUsers <= 1)
	killmonsterall strcharinfo(3);
	return;
}

/// Show current DEADPIT Points
function	script	CurrentPoints	{
	announce "คุณมี " + F_InsertComma(deadPitPoints) + " DEADPIT Points",bc_self,0xD41212;
	return;
}

/// Current potion power
function	script	MyPotionPower	{
	return cap_value(myPotionPower,50,1000000);
}

/// Current potion cooldown
function	script	MyPotionCooldown	{
	return cap_value(10 - myPotionCooldown,1,10);
}

/// Use DEADPIT Potion
function	script	Potion	{
	.@currentTime = gettimetick(2);
	
	if(potionCooldown > .@currentTime)
	{
		message strcharinfo(0),"โปรดรอ " + (potionCooldown - .@currentTime) + " วินาที";
		end;
	}
	
	potionCooldown = .@currentTime + callfunc("MyPotionCooldown");
	
	.@potionPower = callfunc("MyPotionPower");
	
	sc_end SC_POISON;
	sc_end SC_SILENCE;
	sc_end SC_BLIND;
	sc_end SC_CONFUSION;
	sc_end SC_CURSE;
	sc_end SC_Hallucination;
	sc_end SC_BLEEDING;
	sc_end SC_ORCISH;
	sc_end SC_CHANGEUNDEAD;
	sc_end SC_HANDICAPSTATE_DEEPBLIND;
	sc_end SC_HANDICAPSTATE_DEEPSILENCE;
	sc_end SC_HANDICAPSTATE_LASSITUDE;
	sc_end SC_HANDICAPSTATE_CONFLAGRATION;
	sc_end SC_HANDICAPSTATE_MISFORTUNE;
	sc_end SC_HANDICAPSTATE_DEADLYPOISON;
	sc_end SC_HANDICAPSTATE_DEPRESSION;
	sc_end SC_HANDICAPSTATE_HOLYFLAME;
	
	heal .@potionPower,.@potionPower;
	
	specialeffect2 564;
	
	return;
}

/// Use DEADPIT Exiter
function	script	Exiter	{
	if(strcharinfo(3) == "prt_prison"){
		message strcharinfo(0),"ไม่สามารถใช้ได้ในสถานที่นี้";
		end;
	}
	
	specialeffect2 572;
	if((strcharinfo(3) == "nif_dun01")
			|| (strcharinfo(3) == "tra_fild"))
	.@instantWarp = 1;
	if(!.@instantWarp)
	progressbar "0xFBFF00",3;
	callfunc("CleanUpMap");
	warp "nif_dun01",145,140;
	return;
}

/// Lock skill
function	script	AutoSellLockSkill	{
	for(.@i = 0; .@i < $lockSkillSize; .@i++)
	callfunc("SellSkill",$lockSkill$[.@i]);
	return;
}

/// Sell skill
function	script	SellSkill	{
	setarray .@pricingTierLv[0],3,6,9,15,30,50,75,90;
	.@pricingTierLvSize = getarraysize(.@pricingTierLv);
	setarray .@pricingTierPoints[0],10,30,50,90,150,300,500,1000;
	
	.@reward = 0;
	
	.@skillName$ = getarg(0,"");
	
	if(.@skillName$ == "")
	return;

	.@skillLv = getskilllv(.@skillName$);
	
	if(.@skillLv <= 0)
	return;

	for(.@j = .@skillLv; .@j >= 1; .@j--){
		for(.@i = .@pricingTierLvSize - 1; .@i >= 0; .@i--){
			if(.@j >= .@pricingTierLv[.@i]){
				.@reward += .@pricingTierPoints[.@i];
				break;
			}
		}
	}
	
	skill .@skillName$,0,3;
	
	deadPitPoints = cap_value(deadPitPoints + .@reward,0,INT_MAX);
	
	dispbottom "Skill: " + .@skillName$ + " ถูกลบเนื่องจากบัค";
	dispbottom "DEADPIT Points +" + F_InsertComma(.@reward) + " (" + F_InsertComma(deadPitPoints) + ")";
	
	return;
}

function	script	F_IsDummyMonster	{
	.@monsterId = getarg(0,0);
	if((.@monsterId == 2408)										// Dummy
			|| (.@monsterId == 2409)								// Dummy
			|| (.@monsterId == 2410)								// Dummy
			|| (.@monsterId == 2411)								// Dummy
			|| (.@monsterId == 2413)								// Dummy
			|| ((.@monsterId >= 21064) && (.@monsterId <= 21086))	// Dummy
			|| (.@monsterId == 1288)								// Emperium
			|| (.@monsterId == 2337)								// Hidden Mob
			|| (.@monsterId == 2343)								// Hidden Mob
			|| (.@monsterId == 2536)								// Hidden Mob
			|| (.@monsterId == 2537)								// Hidden Mob
			|| (.@monsterId == 2539)								// Hidden Mob
			|| (.@monsterId == 3038)								// Hidden Mob
			)
	return true;
	else
	return false;
}
