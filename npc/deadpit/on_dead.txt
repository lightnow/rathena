-	script	DieEvent	-1,{
OnPCDieEvent:
	// Destroy spawn monster timer
	deltimer "หลุมแห่งความตาย::OnKeepSpawnMonster";
	// Always return to lobby
	if(strcharinfo(3) != "nif_dun01"){
		callfunc("CleanUpMap");
		
		rerollSkillRandomizer = 1;
		
		@deadMessage$ = "";
		
		@deadMessage$ += " รายการ Skill ที่สามารถซื้อได้ ถูกสุ่มใหม่\n";
		
		if(rand(0,100) < .lockSkillPenaltyChance){
			lockSkillPrice = cap_value(lockSkillPrice + rand(1,.lockSkillPenaltyMaximum * BaseLevel),0,.lockSkillPenaltyMaximum * BaseLevel);
			@deadMessage$ += " ติดคำสาปไม่สามารถซื้อ Skill ได้ ค่าปลดคำสาปขณะนี้ " + F_InsertComma(lockSkillPrice) + "\n";
		}
		if(rand(0,100) < .refinePenaltyChance){
			atcommand "@refine 0 -" + .refinePenalty;
			@deadMessage$ += " อุปกรณ์สวมใส่ทุกชิ้นที่สวมใส่อยู่ ค่าตีบวก -1\n";
		}
		if(rand(0,100) < .gradePenaltyChance){
			atcommand "@grade 0 -" + .gradePenalty;
			@deadMessage$ += " อุปกรณ์สวมใส่ทุกชิ้นที่สวมใส่อยู่ ค่า Grade -1\n";
		}
		addtimer 1000,strnpcinfo(3) + "::OnDead";
	}
	
	end;
	
OnDead:
	warp "nif_dun01",145,140;
	addtimer 1000,strnpcinfo(3) + "::OnDeadHint";
	end;
	
OnDeadHint:
	SkillPoint = 0;

	if(@deadMessage$ != "")
	mes "การตายครั้งนี้ส่งผล\n"+@deadMessage$;
	close2;
	message strcharinfo(0),.hints$[rand(0,(.hintsSize - 1))];
	end;
	
OnInit:
	setarray .hints$[0],"เจ้าแห่งความตาย: เจ้าสะดุดก้อนหินตายเหรอ?",
	"เจ้าแห่งความตาย: เฮ้ออออ ไม่เอาไหนเลยนะ..",
	"เจ้าแห่งความตาย: ยังอยากสู้ต่อไหม?",
	"เจ้าแห่งความตาย: พอเถอะน่า..",
	"เจ้าแห่งความตาย: ตายแล้ว.. ตายอีก ตายแล้ว ตายอีก..",
	"เจ้าแห่งความตาย: ต้องฝึกฝนอีกกี่ชาติกันนะ.. เจ้าถึงจะได้กลับมาแบบปกติ ๆ?!",
	"เจ้าแห่งความตาย: ฮ่า ฮ่า ฮ่า! สภาพเจ้าดูไม่ได้เลย!!"
	;
	.hintsSize = getarraysize(.hints$);
	
	.lockSkillPenaltyChance = 75;
	.lockSkillPenaltyMaximum = 2;
	.refinePenaltyChance = 50;
	.refinePenalty = 1;
	.gradePenaltyChance = 50;
	.gradePenalty = 1;
	
	end;
}
